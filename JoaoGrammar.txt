Here is the complete syntax of João in extended BNF. As usual in extended BNF, {A} means 0 or more As, and [A] means an optional A. Brace characters are escaped with single-quotes.
	
	program ::= {classdef | funcdef}
	
	classdef ::= directory '{' {varstat ';'} '}'
	
	funcdef ::= directory '(' [parlist] '){' block '}'
	
	directory ::= '/'{Name'/'} Name
	
	parlist ::= Name {','Name} [',']
	
	varstat :: init_var | (var assignop (exp | directory '/New(' [explist] ')'))
	
	block ::= {stat}
	
	stat ::=  ';' |
		varstat ';' |
		functioncall ';' |
		'break' [PositiveInteger] ';' |
		'while(' exp '){' block '}' |
		'if(' exp '){' block '}' {'elseif(' exp '){' block '}'} ['else(' exp '){' block '}'] |
		'for('[varstat | exp] ';' [exp] ';' [varstat | exp] '){' block '}
	
	init_var :: = ('Value' | 'Object' | 'Number' | 'String' | 'Boolean') Name
	
	var ::= init_var | var_access
	
	var_access ::= scoped_access | var_access property | var_access element
	
	scoped_access ::= './' Name |
		{'../'}'../' Name |
		'/' Name |
		Name
	
	functioncall ::= var_access '(' explist ')'
	
	property ::= {'.' Name }
	
	element ::= {'[' exp ']'}
	
	explist ::= exp {, exp}
	
	exp ::= comparison {('&&' | '||' | '^^') comparison}
	
	comparison ::= bitwise {(‘<’ | ‘<=’ | ‘>’ | ‘>=’ | ‘==’ | ‘!=’) bitwise}
	
	bitwise ::= concat {(‘&’ | ‘~’ | ‘|’ | ‘>>’ | ‘<<’) concat}
	
	concat ::= term {'..' term}
	
	term ::= factor {(‘+’ | ‘-’) factor}
	
	factor ::= unary {(‘*’ | ‘/’ | ‘//’| ‘%’) unary}
	
	unary ::= {unop} power
	
	power ::= {lvalue '^'} lvalue
	
	lvalue ::= 'null' | 'false' | 'true' | Numeral | LiteralString |
			tableconstructor | var_access | functioncall | '(' exp ')' 
	
	tableconstructor ::= '{' [exp] {',' exp} '}' |
		'{' [Name '=' exp] {',' [Name '=' exp] '}'
	
	assignop ::= '=' | '+=' | '-=' | '*=' | '/='

	unop ::= ‘-’ | '!' | ‘#’ | ‘~’